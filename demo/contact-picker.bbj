style! = "
: .root {
:   display: flex;
:   align-items: center;
:   justify-content: center;
:   width: 100vw;
:   height: 100vh;
: }
:  
: .contactsList {
:   padding: var(--dwc-space-m);
: }
:
: dwc-drawer::part(footer){
:   display: none;
: }
:
: .bookEntry {
:   display: flex;
:   gap: 1rem;
:   align-items: center;
:   padding: var(--dwc-space-s);
:   cursor: var(--dwc-cursor-click);
:   transition: background-color var(--dwc-transition);
:   border-bottom: thin solid var(--dwc-color-default) !important;
: }
:
: .bookEntry:hover {
:   background-color: var(--dwc-color-primary-alt);
: }
:
: .bookEntry__avatar {
:   display: flex;
:   align-items: center;
:   width: var(--dwc-size-l);
:   height: var(--dwc-size-l);
:   border-radius: var(--dwc-border-radius-round);
: }
:
: .bookEntry__avatar img {
:   width: 100%;
:   height: 100%;
: }
:
: .bookEntry__info {
:   flex: 1;
:   display: flex;
:   flex-direction: column;
: }
:
: .bookEntry__location {
:   font-size: var(--dwc-font-size-s);
:   color: var(--dwc-color-default-text);
: }  
:"

use ::BBjDrawer/BBjDrawer.bbj::BBjDrawer
use com.basiscomponents.db.ResultSet
use com.basiscomponents.bc.SqlQueryBC

web! = BBjAPI().getWebManager()
web!.injectStyle(style!, 0)

sysgui! =  BBjAPI().openSysGui("X0")

declare auto BBjWindow wnd! 
wnd! = sysgui!.addWindow("BBjDrawer", $01101000$)
wnd!.addPanelStyle("root")
wnd!.setCallback(BBjAPI.ON_CLOSE,"eoj")

declare auto BBjButton contactsButton! 
contactsButton! = wnd!.addButton("Open Contacts Picker")
contactsButton!.setCallback(BBjAPI.ON_BUTTON_PUSH,"onOpenContactsDrawer")
contactsButton!.setAttribute("theme", "primary")
contactsButton!.setAttribute("expanse", "l")

rem Contacts Drawer
rem ==================
declare auto BBjDrawer contactsDrawer! 
contactsDrawer! = new BBjDrawer(wnd!)
contactsDrawer!.setLabel("Contacts")
contactsDrawer!.setPlacement(BBjDrawer.PLACEMENT_BOTTOM_CENTER)
contactsDrawer!.setSize("70vh")

declare auto BBjChildWindow contactsDrawer! 
content! = contactsDrawer!.getContent()
content!.addStyle("contactsList")

sql! = new SqlQueryBC(BBjAPI().getJDBCConnection("ChileCompany"))
items! = sql!.retrieve("SELECT * FROM CUSTOMER")
iterator! = items!.iterator()

while iterator!.hasNext()
  item! = iterator!.next()
  firstName! = item!.getField("FIRST_NAME").getString().trim()
  lastName! = item!.getField("LAST_NAME").getString().trim()
  fullName! = firstName! + " " + lastName!
  phone! = item!.getField("PHONE").getString().trim()
  country! = item!.getField("COUNTRY").getString().trim()
  city! = item!.getField("CITY").getString().trim()
  fullLocation! = country! + " - " + city!

  card! = content!.addChildWindow("", $00108800$, sysgui!.getAvailableContext())
  card!.addStyle("bookEntry")

  avatarContent! = "<html><img src=""https://ui-avatars.com/api/?name=" + fullName! + "&&background=random"" /></html>"
  avatar! = card!.addStaticText(avatarContent!)
  avatar!.addStyle("bookEntry__avatar")

  info! = card!.addChildWindow("", $00108800$, sysgui!.getAvailableContext())
  info!.addStyle("bookEntry__info")

  name! = info!.addStaticText(fullName!)
  name!.addStyle("bookEntry__name")

  location! = info!.addStaticText(fullLocation!)
  location!.addStyle("bookEntry__location")

  call! = card!.addButton("<html><dwc-icon name=""phone""></dwc-icon></html>")
  call!.setEnabled(len(phone!) > 0)
  call!.setUserData(phone!)
  call!.setCallback(call!.ON_BUTTON_PUSH, "onCall")
wend

wait 1
contactsDrawer!.open()

process_events

onOpenContactsDrawer:
  contactsDrawer!.open()
return

onCall:
  contactsDrawer!.close()
  
  ev! = BBjAPI().getLastEvent()
  control! = ev!.getControl()
  phone! = str(control!.getUserData())
  script! = "" +
:    "(() => {" +
:    " const link = document.createElement('a');" +
:    " link.href='tel:" + phone! + "';" +
:    " link.style.visibility='hidden';" +
:    " document.body.appendChild(link);" +
:    " link.click();" +
:    " document.body.removeChild(link)" +
:    "})()"
  BBjAPI().getSysGui().executeScript(script!)
return

eoj:
release
